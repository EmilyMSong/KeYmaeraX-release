ArchiveEntry "Robot Component"

Definitions
  Real ep;   /* time limit for control decisions */
  Real B;    /* minimum braking capability of the robot */
  Real A;    /* maximum acceleration -B <= a <= A */
  Real V;    /* maximum velocity of obstacles */
End.

ProgramVariables
  Real x;    /* robot position: x */
  Real y;    /* robot position: y */
  Real v;    /* robot translational velocity */
  Real a;    /* robot translational acceleration */
  Real dx;   /* robot orientation: x */
  Real dy;   /* robot orientation: y */
  Real w;    /* robot rotational velocity */
  Real k;    /* curvature of curve (i)straight: k=0; (ii)curve: k!=0 */
  Real xo;   /* position summary of obstacles */
  Real yo;
  Real t;    /* time */
  Real xo0;
  Real yo0;
End.

Problem
    v >= 0
  & ( abs(x-xo) > v^2 / (2*B) + V*(v/B)
    | abs(y-yo) > v^2 / (2*B) + V*(v/B))
  & dx^2 + dy^2 = 1
  & A >= 0
  & B > 0
  & V >= 0
  & ep > 0
  & xo0 = xo
  & yo0 = yo
  & t = 0
  ->
  [
    {

      /* ctrl */
      {
        /* brake on current curve or remain stopped */
        { a := -B; }
        ++
        { ?v = 0; a := 0; w := 0; }
        ++
        /* or choose a new safe curve */
        { a :=*; ?-B <= a & a <= A;
          k :=*;
          w :=*; ?v * k = w;

          /* use that curve, if it is a safe one (admissible velocities) */
          ? abs(x-xo) > v^2/(2*B) + V*v/B + (A/B + 1) * (A/2 * ep^2 + ep*(v+V))
          | abs(y-yo) > v^2/(2*B) + V*v/B + (A/B + 1) * (A/2 * ep^2 + ep*(v+V));
        }
      };

      xo0 := xo;
      yo0 := yo;

    	t := 0;

      /* dynamics */
      { x' = v * dx,
        y' = v * dy,
        dx' = -w * dy,
        dy' = w * dx,
        v' = a,
        w' = a*k,
        t' = 1 & t <= ep & v >= 0
      }

      /* ports */
      xo :=*; ?-V*t <= xo-xo0 & xo-xo0 <= V*t;
      yo :=*; ?-V*t <= yo-yo0 & yo-yo0 <= V*t;
    }*
  ] (v > 0 -> (x - xo)^2 + (y - yo)^2 > 0)
End.

Tactic "Proof"
implyR(1) ; andL('L)* ; loop({`v>=0&dx^2+dy^2=1&0<=t&t<=ep()&-V()*t<=xo-xo0&xo-xo0<=V()*t&-V()*t<=yo-yo0&yo-yo0<=V()*t&(v=0|abs(x-xo)>v^2/(2*B())+V()*(v/B())|abs(y-yo)>v^2/(2*B())+V()*(v/B()))`}, 1) ; <(
  QE,
  QE,
  unfold ; <(
    diffInvariant({`0<=t`}, 1) ; diffInvariant({`dx^2+dy^2=1`}, 1) ; diffInvariant({`v=old(v)-B()*t`}, 1) ;
    diffInvariant({`-t*(v+B()/2*t)<=x-old(x)&x-old(x)<=t*(v+B()/2*t)`}, 1) ;
    diffInvariant({`-t*(v+B()/2*t)<=y-old(y)&y-old(y)<=t*(v+B()/2*t)`}, 1) ; dW(1) ; unfold ; simplify(1) ;
    orR(1) ; orR(2) ; orL(-14) ; <(
      hideR(2=={`abs(x-xo)>v^2/(2*B())+V()*(v/B())`}) ; hideR(2=={`abs(y-yo)>v^2/(2*B())+V()*(v/B())`}) ; QE,
      orL(-14) ; <(
        hideR(3=={`abs(y-yo)>v^2/(2*B())+V()*(v/B())`}) ; smartQE,
        hideR(2=={`abs(x-xo)>v^2/(2*B())+V()*(v/B())`}) ; smartQE
        )
      ),
    diffInvariant({`0<=t`}, 1) ; diffInvariant({`v=old(v)`}, 1) ; allR2L(-19) ; diffInvariant({`dx^2+dy^2=1`}, 1) ;
    dW(1) ; unfold ; hideR(2) ; hideR(2) ; smartQE,
    diffInvariant({`0<=t`}, 1) ; diffInvariant({`dx^2+dy^2=1`}, 1) ; diffInvariant({`v=old(v)+a*t`}, 1) ;
    diffInvariant({`-t*(v-a/2*t)<=x-old(x)&x-old(x)<=t*(v-a/2*t)`}, 1) ;
    diffInvariant({`-t*(v-a/2*t)<=y-old(y)&y-old(y)<=t*(v-a/2*t)`}, 1) ; dW(1) ; unfold ; simplify(1) ; orR(1) ;
    orR(2) ; hideL(-17=={`v_0=0|abs(x_0-xo_0)>v_0^2/(2*B())+V()*(v_0/B())|abs(y_0-yo_0)>v_0^2/(2*B())+V()*(v_0/B())`}) ;
    hideL(-22=={`dx^2+dy^2=1`}) ; orL(-7) ; <(
      hideR(3=={`abs(y-yo)>v^2/(2*B())+V()*(v/B())`}) ; hideR(1=={`v=0`}) ; hideL(-15=={`-V()*t_0<=yo_0-yo0_0`}) ;
      hideL(-15=={`yo_0-yo0_0<=V()*t_0`}) ; hideL(-15=={`-t*(v-a/2*t)<=y-y_0`}) ; hideL(-15=={`y-y_0<=t*(v-a/2*t)`}) ;
      hideL(-23=={`-V()*t<=yo-yo0`}) ; hideL(-23=={`yo-yo0<=V()*t`}) ; hideL(-9=={`yo0=yo_0`}) ;
      edit({`abs(x_0-xo_0)>v_0^2/(2*B())+V()*v_0/B()+(A()/B()+1)*(A()/2*t^2+t*(v_0+V()))`}, -7) ;
      hideL(-11=={`t_0<=ep()`}) ; hideL(-17=={`t<=ep()`}) ; hideL(-4=={`ep()>0`}) ; smartQE
      ,
      hideR(2=={`abs(x-xo)>v^2/(2*B())+V()*(v/B())`}) ; hideL(-8=={`xo0=xo_0`}) ; hideL(-12=={`-V()*t_0<=xo_0-xo0_0`}) ;
      hideL(-12=={`xo_0-xo0_0<=V()*t_0`}) ; hideL(-16=={`-t*(v-a/2*t)<=x-x_0`}) ; hideL(-16=={`x-x_0<=t*(v-a/2*t)`}) ;
      hideL(-20=={`-V()*t<=xo-xo0`}) ; hideL(-20=={`xo-xo0<=V()*t`}) ;
      edit({`abs(y_0-yo_0)>v_0^2/(2*B())+V()*v_0/B()+(A()/B()+1)*(A()/2*t^2+t*(v_0+V()))`}, -7) ;
      hideL(-10=={`0<=t_0`}) ; hideL(-10=={`t_0<=ep()`}) ; hideL(-16=={`t<=ep()`}) ; hideL(-4=={`ep()>0`}) ; smartQE
      )
    )
  )
End.

End.